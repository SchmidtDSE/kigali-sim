start default

  define application "Domestic Refrigeration"

    uses substance "HFC-134a"
      enable domestic
      initial charge with 0.15 kg / unit for domestic
      initial charge with 0 kg / unit for import
      initial charge with 0 kg / unit for export
      equals 1430 kgCO2e / kg
      equals 1 kwh / unit
      set priorEquipment to 1000000 units during year 2025
      set domestic to 24 mt / year during year 2025
      change sales by 6 % / year during years 2025 to 2030
      change sales by 4 % / year during years 2031 to 2035
      retire 5 % / year
      recharge 10 % with 0.15 kg / unit
    end substance

  end application

end default


start policy "Sales Permit"

  modify application "Domestic Refrigeration"

    modify substance "HFC-134a"
      cap sales to 85 % during years 2029 to 2034
      cap sales to 0 mt during years 2035 to onwards
    end substance

  end application

end policy


start policy "Domestic Recycling"

  modify application "Domestic Refrigeration"

    modify substance "HFC-134a"
      recover 20 % with 90 % reuse with 100 % induction during years 2027 to onwards
    end substance

  end application

end policy


start simulations

  simulate "BAU"
  from years 2025 to 2035


  simulate "Permit"
    using "Sales Permit"
  from years 2025 to 2035


  simulate "Recycling"
    using "Domestic Recycling"
  from years 2025 to 2035


  simulate "Recycling First"
    using "Domestic Recycling"
    then "Sales Permit"
  from years 2025 to 2035


  simulate "Permit First"
    using "Sales Permit"
    then "Domestic Recycling"
  from years 2025 to 2035

end simulations
