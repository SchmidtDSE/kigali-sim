start default

  define application "Domestic Refrigeration"

    uses substance "HFC-134a"
      set priorEquipment to 1200000 units during year 2025
      initial charge with 0.15 kg / unit for sales
      retire 2 % each year
      recharge 10 % each year with 0.15 kg / unit
      set manufacture to 30000 units / year during year 2025
      change manufacture by +5 % each year during years 2026 to 2033
      change manufacture by +3 % each year during years 2034 to 2035
      equals 1430 tCO2e / kg
    end substance

  end application

  define application "Commercial Refrigeration"

    uses substance "HFC-134a"
      set priorEquipment to 240000 units during year 2025
      initial charge with 0.60 kg / unit for sales
      retire 3 % each year
      recharge 30 % each year with 0.60 kg / unit
      set import to 2667 units / year during year 2025
      change import by +8 % each year during years 2026 to 2033
      change import by +5 % each year during years 2034 to 2035
      equals 1430 tCO2e / kg
    end substance

  end application

  define application "Residential AC"

    uses substance "HFC-32"
      set priorEquipment to 24000 units during year 2025
      initial charge with 0.85 kg / unit for sales
      retire 1 % each year
      recharge 10 % each year with 0.85 kg / unit
      set import to 800 units / year during year 2025
      change import by +7 % each year during years 2026 to 2035
      equals 675 tCO2e / kg
    end substance

    uses substance "R-410A"
      set priorEquipment to 400000 units during year 2025
      initial charge with 1.00 kg / unit for sales
      retire 1 % each year
      recharge 10 % each year with 1.00 kg / unit
      set import to 100 units / year during year 2025
      set manufacture to 40000 units / year during year 2025
      equals 2088 tCO2e / kg
    end substance

  end application

end default


start policy "Manufacturing Prohibition"

  modify application "Domestic Refrigeration"
    modify substance "HFC-134a"
      cap manufacture to 0 % during years 2028 to onwards
    end substance
  end application

  modify application "Residential AC"
    modify substance "R-410A"
      cap manufacture to 0 % during years 2028 to onwards
    end substance
  end application

end policy


start policy "Import Restrictions"

  modify application "Commercial Refrigeration"
    modify substance "HFC-134a"
      cap import to 75 % during year 2029
      cap import to 50 % during year 2030
      cap import to 0 % during years 2031 to onwards
    end substance
  end application

  modify application "Residential AC"
    modify substance "HFC-32"
      cap import to 0 % during years 2029 to onwards
    end substance
  end application

end policy


start policy "Recovery Program"

  modify application "Domestic Refrigeration"
    modify substance "HFC-134a"
      recover 20 % with 100 % reuse during years 2030 to onwards
    end substance
  end application

  modify application "Commercial Refrigeration"
    modify substance "HFC-134a"
      recover 20 % with 100 % reuse during years 2030 to onwards
    end substance
  end application

end policy


start simulations

  simulate "Business as Usual" from years 2025 to 2035

  simulate "Manufacturing Prohibition" using "Manufacturing Prohibition" from years 2025 to 2035

  simulate "Import Restrictions" using "Import Restrictions" from years 2025 to 2035

  simulate "Recovery Program" using "Recovery Program" from years 2025 to 2035

  simulate "Combined Policies" 
    using "Manufacturing Prohibition" 
    then "Import Restrictions" 
    then "Recovery Program" 
  from years 2025 to 2035

end simulations